plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'signing'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'ru.vyarus.quality' version '4.9.0'
    id 'net.researchgate.release' version '3.0.2'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'ru.vyarus.mkdocs' version '3.0.0'
}

sourceCompatibility = 1.7

wrapper {
    gradleVersion = '7.5.1'
}

repositories { mavenLocal(); mavenCentral() }
dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.6'

    // using old groovy because it's more permissive and allow testing more edge cases
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

group = 'ru.vyarus'
description = 'Java generics runtime resolver'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs.publish {
    docPath = '3.0.3'
    rootRedirect = true
    rootRedirectTo = 'latest'
    versionAliases = ['latest']
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

javaLib {
    // java 9 auto module name
    autoModuleName = "$project.group.$project.name"
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = ['publishToSonatype',
                 'closeAndReleaseSonatypeStagingRepository']
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
//    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
