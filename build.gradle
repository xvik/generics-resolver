plugins {
    id 'com.gradle.build-scan' version '3.0'
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id 'ru.vyarus.animalsniffer' version '1.5.0'
    id 'ru.vyarus.quality' version '3.3.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'ru.vyarus.mkdocs' version '1.1.0'
}

sourceCompatibility = 1.6

wrapper {
    gradleVersion = '5.6.4'
}

ext {
    groovy = '2.5.9'
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.2'
    implementation  'org.apache.commons:commons-lang3:3.9'
    signature 'org.codehaus.mojo.signature:java16:1.1@signature'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    // groovy bundled with spock (2.5.4) does not work well with java 11
    testImplementation "org.codehaus.groovy:groovy-all:$groovy"
}

group = 'ru.vyarus'
description = 'Java generics runtime resolver'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs.publish {
    docPath = '3.0.2'
    rootRedirect = true
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

// java 11 auto module name
jar {
    manifest {
        attributes 'Automatic-Module-Name': "$project.group.$project.name"
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['java', 'generics']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = true
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishOnFailure()
}
